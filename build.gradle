plugins {
    id 'java'
    id 'checkstyle'
}

group = 'org.example'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

def testngVersion = "7.10.2"
def selenideVersion = "7.11.1"
def allureVersion = "2.29.1"
def aspectJVersion = "1.9.25"
def slf4jVersion = "2.0.9"
def logbackVersion = "1.4.14"
def apacheCommonsVersion = "3.0"
def restAssuredVersion = "5.4.0"
def lombokVersion = "1.18.32"
def jacksonVersion = "2.20.1"
def fakerVersion = "1.0.2"
def reportPortalAllureAgentVersion = "5.4.0"

configurations {
    agent {
        canBeResolved = true
        canBeConsumed = true
    }
}

def isCi = System.getenv('CI')?.toBoolean() ?: false

dependencies {
    testImplementation "org.testng:testng:$testngVersion"
    testImplementation "com.codeborne:selenide:$selenideVersion"
    testImplementation platform("io.qameta.allure:allure-bom:$allureVersion")
    testImplementation "io.qameta.allure:allure-testng"
    agent "org.aspectj:aspectjweaver:$aspectJVersion"
    testImplementation "org.slf4j:slf4j-api:$slf4jVersion"
    testRuntimeOnly "ch.qos.logback:logback-classic:$logbackVersion"
    checkstyle "com.puppycrawl.tools:checkstyle:${checkstyle.toolVersion}"
    testImplementation "org.apache.commons:commons-lang3:$apacheCommonsVersion"
    testImplementation "io.rest-assured:rest-assured:$restAssuredVersion"
    testCompileOnly "org.projectlombok:lombok:$lombokVersion"
    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
    testImplementation "io.qameta.allure:allure-rest-assured"
    testImplementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    testImplementation "com.github.javafaker:javafaker:$fakerVersion"
    if (isCi) {
        testImplementation "com.epam.reportportal:agent-allure-testng:$reportPortalAllureAgentVersion"
        agent "org.aspectj:aspectjweaver:$aspectJVersion"
    }
}

checkstyle {
    toolVersion = '10.14.0'
    configFile = file("config/checkstyle/checkstyle.xml")
}

test {
    def groups = System.getProperty('groups.include') ?: null

    useTestNG {
        suiteName = "JuicyShopAutomation Tests"
        if (groups) {
            includeGroups groups
        }
        systemProperty "testng.listeners", "io.qameta.allure.testng.AllureTestNg"
        if (isCi) {
            systemProperty "reportPortal.listeners", "com.epam.reportportal.testng.ReportPortalTestNGListener"
        }
    }

    testLogging {
        events "passed", "skipped", "failed"
    }

    doFirst {
        if (isCi) {
            def weaver = configurations.agent.singleFile
            jvmArgs += "-javaagent:$weaver"
        }
    }
}