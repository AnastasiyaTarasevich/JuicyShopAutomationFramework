plugins {
    id 'java'
    id 'checkstyle'
}

group = 'org.example'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

def testngVersion = "7.10.2"
def selenideVersion = "7.11.1"
def allureVersion = "2.29.1"
def slf4jVersion = "2.0.9"
def logbackVersion = "1.4.14"
def apacheCommonsVersion = "3.0"
def restAssuredVersion = "5.4.0"
def lombokVersion = "1.18.32"
def jacksonVersion = "2.20.1"
def fakerVersion = "1.0.2"
def aspectjVersion = "1.9.22"

configurations {
    agent {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    testImplementation "org.testng:testng:$testngVersion"
    testImplementation "com.codeborne:selenide:$selenideVersion"
    testImplementation platform("io.qameta.allure:allure-bom:$allureVersion")
    testImplementation "io.qameta.allure:allure-testng"
    testImplementation "io.qameta.allure:allure-rest-assured"
    testImplementation "org.slf4j:slf4j-api:$slf4jVersion"
    testRuntimeOnly "ch.qos.logback:logback-classic:$logbackVersion"
    testImplementation "org.apache.commons:commons-lang3:$apacheCommonsVersion"
    testImplementation "io.rest-assured:rest-assured:$restAssuredVersion"
    testCompileOnly "org.projectlombok:lombok:$lombokVersion"
    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
    testImplementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    testImplementation "com.github.javafaker:javafaker:$fakerVersion"
    testImplementation "com.epam.reportportal:agent-java-testng:5.6.5"
    agent "org.aspectj:aspectjweaver:$aspectjVersion"
}

checkstyle {
    toolVersion = '10.12.0'
    configFile = file("config/checkstyle/checkstyle.xml")
}

test {
    def groups = System.getProperty('groups.include') ?: null

    useTestNG {
        suiteName = "JuicyShopAutomation Tests"
        if (groups) {
            includeGroups groups
        }
        listeners << "io.qameta.allure.testng.AllureTestNg"
    }

    testLogging {
        events "passed", "skipped", "failed"
    }

    jvmArgs += "-javaagent:${configurations.agent.singleFile}"
}
